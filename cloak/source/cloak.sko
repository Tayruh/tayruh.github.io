## go
	= return
	+ {go_return} [Back];; >> #$:room

	= back
	+ {go_back} [Back];; >> $:bookmark

	= inventory
	+ {go_inv} [Back];; >> #inventory

	
	## start
		[:& 
			$.message = 0;
			$.cloak_worn = true;
			&.add($.items, "cloak");
		:]
		
		Hurrying through the rainswept November night, you're glad to see the bright lights of the Opera House. It's surprising that there aren't more people about but, hey, what do you expect in a cheap demo game...
		
		<i>This demo is based on <a class="link" target="_blank" href="http://www.firthworks.com/roger/cloak/">Cloak of Darkness</a> by Roger Firth.</i>
		
		+ {a} [Start];; >> #foyer
		

	## inventory 
		[:= game.listInventory():]
		

	## foyer ~:room:Foyer
		[:& _.foyer = (&.has($.items, "cloak")) ? "darkness" : "foyer_bar":]
		You are standing in a spacious hall, splendidly decorated in red and gold, with glittering chandeliers overhead. The entrance from the street is to the [:% north:], and there are doorways [:#= _.foyer @: south:] and [:cloakroom @: west:].
		
		+ {ent} [Entrance] :: %.foyer.north
			>> north
		+ {bar} [Foyer Bar] :: #.foyer_bar
			>># _:foyer
		+ {clk} [Cloakroom] :: #.cloakroom
			>> #cloakroom
		- << END
	   

		= north
		You've only just arrived, and besides, the weather outside seems to be getting worse. 
		+ >>= go.return
	   

	## cloakroom ~:room:Cloakroom
		The walls of this small room were clearly once lined with [:% hooks:], though now only one remains. The exit is a door to the [:foyer @: east:].
		[:& $.hooks = false:]
		
		+ {fyr} [Foyer];; >> #foyer

	   
		= hooks
		(:bookmark "cloakroom.hooks":)
		[:& $.hooks = true:]
		It's just a small brass hook, <> 
		{:&.has($.items, "cloak")::screwed to the wall.::with a [:% cloak.take @: cloak:] hanging on it.:}
		+ >>= go.return


	## cloak
		>> examine
		<< END
		

		= desc
		A handsome cloak, of velvet trimmed with satin, and slightly spattered with raindrops. Its blackness is so deep that it almost seems to suck light from the room.
		<<
		

		= examine
		>> desc
		+ [Wear] :: !$.cloak_worn
			You put on the cloak.
			[:& $.cloak_worn = true:]
		+ [Remove] :: $.cloak_worn
			You remove the cloak.
			[:& $.cloak_worn = false:]
		+ [Hang] :: $.room === "cloakroom"
			You put the velvet cloak on the small brass hook.
			[:& 
				$.cloak_worn = false;
				&.remove($.items, "cloak");
				&.onDialogClose = function() { if ($.hooks) &.doLink('cloakroom.hooks'); }
			:]
			++ [Back];; [:*!:]
				>> $:bookmark
		+ [Drop] :: $.room !== "cloakroom"
			This isn't the best place to leave a smart cloak lying around.
		-
		+ >>= go.inventory


		= take
		>> desc
		+ [Take Cloak] :: !&.has($.items, "cloak")
			[:& &.add($.items, "cloak"):]
			You remove the cloak from the hook.
			++ >>= go.back
		+ >>= go.back


	## darkness ~:room:Darkness
		It is pitch dark, and you can't see a thing.
		
		+ {n} [North];; >> #foyer
		+ {s} [South]
		+ {e} [East]
		+ {w} [West]
		- 
		In the dark? You could easily disturb something! :: $.message === 0
		Blundering around in the dark isn't a good idea! :: $.message >= 1
		[:& $.message += 1:]
		+ >>= go.return


	## foyer_bar ~:room:Foyer Bar
		The bar, much rougher than you'd have guessed after the opulence of the foyer to the [:foyer @: north:], is completely empty. There seems to be some sort of [:% message:] scrawled in the sawdust on the floor.
		
		+ {fyr} [Foyer];; >> #foyer


		= message
		~ if ($.message < 2)
			The message, neatly marked in the sawdust, reads...
			\*** You Won! ***
		~ else
			The message has been carelessly trampled, making it difficult to read. You can just distinguish the words...
			\*** You Lost! ***
