## inventory
	[:= game.displayInventory():]


## notebook
	= tasks
	[:& game.notebook_tab = "tasks":]
	>> index
	[:= game.listTasks():]
	>> go.inventory

	= characters
	[:& game.notebook_tab = "characters":]
	>> index
	[:= game.listCharacters():]
	>> go.inventory

	= index
	<a id="tasks" onclick='sadako.doLink("notebook.tasks")'>Tasks</a><>
	<a id="characters" onclick='sadako.doLink("notebook.characters")'>Characters</a><>
	<hr> ~:class:notebook
	[:&
		setTimeout(function() {
			sadako.addClass("#" + game.notebook_tab, "active");
			sadako.dom("#" + game.notebook_tab).onclick = null;
		}, 0);
	:]
	<<


## go
	= return
	[:$:room @: Go back.:] ~:choice
	<< END

	= eoc
	[:$:bookmark @: End the conversation.:] ~:choice
	<< END

	= back
	[:$:bookmark @: Go back.:] ~:choice
	<< END

	= inventory
	[:inventory @: Back:] ~:choice
	<< END


## start
	~ if (game.debug)
		[:&
			$.name = "Sion";
			$.lname = "Barzahd";
		:]
		>> name_chosen

	- Welcome.
	Before we start, I would like you to name your character. In this story you will be playing the role of a man in their early twenties.

	= enter_name
	Please tell me your name.

	 <table><>
		 <tr><>
			<td>[:> $.name @: <b>First</b>^^:]</td><>
			<td>[:> $.lname @: <b>Last</b>^^:]</td><>
		 </tr><>
	 </table>

	+ [Continue]
		~~ if (!$.name)
			Name is invalid.
			>> enter_name
		-- Your name is $:name $:lname. Is this correct?
		++ {y} [Yes]
		++ [No];; >> enter_name

	= name_chosen
	[:&
		$.fname = "$:name $:lname";
		game.move("items", "notebook");
		$.erin_following = false;
	:]
	>> #office :: game.debug

	= intro
	(:bookmark "intro":)
	Thank you.
	Before play begins, I should explain a bit how this story works.
	Any links in the page description like [:%example @: this one:] will be for examining only. They will never be actions that progress the story.
	Links at the bottom of the page will be either dialogue to say or actions to perform.
	The inventory (<img title="inventory" class="inline-icon" src="icons/inventory.png">) button is at the left of the title bar located at the top of the screen. This button will display an inventory menu. Any new items you pick up during the story will get added to your inventory.
	In this story, you already start with a notebook which keeps track of tasks and character information. As you gain and complete tasks, your task menu will be automatically updated to help you track progress. Any new information you learn about characters will be added to the character menu.

	[:%story_start @: Begin the story.:] ~:choice
	<< END

	= example
	This was an example link.
	Normally this would be the description of a room object or the like. If you could interact with the object, the action would be listed as a link below.
	>> go.back

	= story_start
	(:setCharInfo "erin", "Erin":)
	<center><h2>MONSTER</h2></center> ~:delay:1250
	- ~:delay:0
	You and your sister Erin finally arrive at the front steps. It's been a long flight here and you're both exhausted.
	"Are you ready for this?" Erin sighs, staring up at the large mansion.

	+ "I'm not even sure what <i>this</i> is."
	-

	She laughs. "Too true. Dad calls us up out of nowhere--we hadn't even talked to him in years--and then asks us to hop a plane and come down here to help him with his business." Erin shakes her head. "Why'd we even come?"

	+ "He sounded like he needed help."
		"That's true. I can't remember him ever asking for help before. It must be serious."
	+ "You seemed like you wanted to go."
		"Nah. Well.. Yeah. I just didn't have anything going on and it seemed interesting. I'm glad you came to back me up."
	+ "I had nothing better to do."
		"Yeah, same. And I wasn't going to let you deal with whatever <i>this is</i> on your own. We're a team," she says, smiling.
	-
	+ "What was his business anyway? A boarding house in this huge place?"

	- "Yeah," she says, looking up at the structure. "Something like that. He was really vague on the details, but basically he's a landlord, I guess? I have no idea what he'd need help with though."

	-
	+ "I guess we should head in then."
	-
	(:setCharInfo "walter", "Father":)
	"Might as well." She shrugs and heads up the front steps.
	You follow her up the steps and into the building. Your father is already standing on the other side of the door to greet you.
	"Finally," he snaps. "You two, come with me." Without saying another word, he turns and immediately heads toward the hall.
	"Nice to see you too, Dad," Erin grumbles.
	The two of you leave your luggage near the entryway and follow him into a door at the opposite end of the foyer that leads into a small office. He closes the door behind you and then stands behind a wooden desk at the back of the room.
	"Sit," he says as if commanding a puppy.

	+ "Can you tell us what this is about?"
		"I'm about to get to that. Have a seat first."
	+ [Take a seat.]
	-

	You both take seats facing your father. Erin remains quiet.
	"Thank you for coming." He pauses while looking the two of you over. "I know it's been some time since we last made contact. During that time I have acquired this estate and become invested in various other endeavors." He places a large bundle of keys on the desk. "Now then, I have business in another country that requires my immediate attention. I am leaving the estate in your care."
	Your father moves from behind to the desk and heads toward the door. As he walks past you, your sister calls out. "<i>Wait!</i> That's <i>it</i>? What the hell is going on!"
	"My apologies but I do not have the time. Vanessa can assist you with any questions you may have. Do not contact me unless it is an emergency."
	"<i>Who the hell is Vanessa!</i>" Erin demands.
	"I am placing my trust in you," your father finishes, ignoring your sister's outburst. And with that, he closes the door behind him and is gone.
	Erin's mouth is agape as she stares wide-eyed at the office door.

	+ {start} [Continue]
		(:addTask "main_1.find_vanessa":)
		>> #office


## office ~:room:Office
	This is (was) your father's office. As such, it's meticulously organized. Sterile, even. There's a large [:%desk:] at the back of the room and [:%cabinets @: filing cabinets:] placed against one wall. The room is slightly cramped and feels even more claustrophobic due to its lack of any windows.

	On the desk is a large set of [: master_keys @: keys :]. :: !%.master_keys.take

	[:%erin.intro @: Erin:] is seated in a chair beside you, a look of disbelief on her face. :: !#.foyer

	+ [Exit the office.] :: !#.foyer
		(:clearTitle:)
		As you leave the office, something small flies past your vision and disappears around the corner leading to the hallway on the right.

		"What the heck was that!" Erin shouts. "That wasn't a bird, or a bug. What was that?"

		++ "Should we try to catch it?"
		-- "No. No, I'm good on that. Let's just.. keep exploring."
		++ {fairy} [Continue]
			[:& $.erin_following = true:]
			>> #foyer

	+ {foyer} [Enter the foyer.] :: #.foyer
		>> #foyer

	= desk
	(:bookmark "desk":)
	Your father's desk is dust-free and organized. There are a few [:%papers:] on the desk, but even those are collected in a neat stack in the corner of the desk.
	There is a large set of [: master_keys @: keys :] on the desk. :: !%.master_keys.take
	>> go.return

	= papers
	There is a small stack of papers organized neatly on the desk. They look very official and most likely relate to the estate.
	>> go.back

	= cabinets
	(:bookmark "cabinets":)
	The small filing cabinet rests comfortably against the wall. It just has one large drawer.
	+ {search_cabinet} [Search the drawer.];; >> cabinet_drawer
	+ {cabinet_back} [Go back.];; << RETURN

	= cabinet_drawer
	~ if (!%.office.cabinet_unlocked)
		The cabinet drawer appears to be locked.
		Perhaps you can use one of the keys on the your key ring. :: %.master_keys.take
	~ else
		~~ if (%.daemon.intro_talk)
			Most of the files in the cabinet are just legal mumbo jumbo that you're not sure how to make sense of. However, one of the pages is a deed to the mansion. It reads..
			
			I, Walter $:lname, of Octavia, New Haven, hereby transfer ownership of the $:lname Estate to $:fname and Erin $:lname as witnessed by Vanessa. ~:class:article
			
			~~~ if ($.erin_following && !%.office.erin_deed)
				{erin_deed} Erin nearly rips the paper out of your hand to read it for herself. 
				"<i>He gave us the estate!</i>" she shouts the question in shock. "So.. what? He's never coming back? Did he leave us <i>again</i>?"
				++++ "We should ask Vanessa about this."
					"For real."
		~~ else if ($.erin_following || !#.foyer)
			You're about to open the drawer when Erin interrupts you. "Do we have time for that? We should probably go talk to the others first."
		~~ else
			You open the drawer and peer at the files. There are so many files and you still haven't found Erin yet. Finding your sister takes priority.
	- >> go.back

	= cabinet_unlocked
	(:closeDialog:)
	You unlock the file cabinet drawer with a small key.
	>> cabinet_drawer


## foyer ~:room:Foyer
	The foyer is wide and spacious. There are a couple of [:%couches:] and other pieces of furniture for guests to relax on, but mostly this room is just the entranceway with adjoining hallways. There are [:%stairs:] leading up and [:%hallways:] to the east and west, along with the [:%entrance @: entrance door:] leading outside.

	Your and your sister's [:%luggage:] is left haphazardly near the entrance door. :: !%.daemon.intro_talk

	~ if (%.sera.intro && !%.foyer.intro_no_spider)
		{intro_no_spider} You don't see any sign of {:%.daemon.intro_talk::Sera::the spider girl:}. It seems she has already scurried away.

	+ {office} [Enter the office.];; >> #office
	+ {dh} [Enter the east hallway.];; >> #east_hallway
	+ {up} [Climb the stairs to the second floor.];; >> #2F_hallway

	= couches
	The couches face one another and are placed parallel to the entrance door. The cushions are covered in a smooth fabric and are quite soft to the touch. They make for very comfortable seats.
	>> go.back

	= hallways
	There are hallways on either side of the foyer, with the office centered between them.
	<> There appears be some sort of noise coming from east hallway. It's difficult to make out. :: !#.kitchen
	>> go.back

	= entrance
	Two large oak doors lead to the front steps of the mansion. The doors are a couple feet taller than should be needed for any average person to enter.
	>> go.back

	= stairs
	The large set of stairs are located just to the side of the west hallway and run up along the wall, ending in a landing that leads to a hallway above you.
	>> go.back

	= luggage
	You brought very little with you compared to your sister. It looks like she may have packed her entire room in there. You may not know when you are to return, but it appears as though she is planning on taking up permanent residence.
	At some point you'll have to get these bags to your rooms. :: $.room === "foyer"
	>> go.back


/* === East Hallway === */

## east_hallway ~:room:East Hallway
	[:&
		var rooms = sadako.find([["kitchen","kitchen"], ["dining_room","dining room"], ["bar","bar"], ["living_room", "living room"]], function(x) {
			return sadako.page_seen[x[0]];
		});
		~~="This hallway contains ";
		if (!rooms.length) ~~+"four doors."
		else {
			~~+"a door leading to ";
			if (rooms.length === 1) ~~+"the " + rooms[0][1] + " and three other doors.";
			else if (rooms.length === 2)  ~~+"the " + rooms[0][1] + ", the " + rooms[1][1] + ", and two other doors.";
			else if (rooms.length === 3)  ~~+"the " + rooms[0][1] + ", the " + rooms[1][1] + ", the " + rooms[2][1] + ", and one other door.";
			else ~~+"the kitchen, the dining room, the bar, and the living room.";
		}
	:]

	<> The doors are located on one side of the hall, opposite the wall separating the foyer and the office. The end of the hall wraps around into the back hallway.

	There is the sound of running water and clinking glass coming from behind the first door. :: !#.kitchen

	+ {foyer} [Enter the foyer.]
		>> sera.intro :: !%.sera.intro && %.seoyun.intro_talk && %.finola.intro_talk
		>> #foyer
	+ {ki} [Enter the {:#.kitchen::kitchen::first door:}.];; >> #kitchen
	+ {dr} [Enter the {:#.dining_room::dining room::second door:}.];; >> #dining_room
	+ {bar} [Enter the {:#.bar::bar::third door:}.];; >> #bar
	+ {lr} [Enter the {:#.living_room::living room::fourth door:}.];; >> #living_room
	+ {bw} [Enter the back hallway.];; >> #back_hallway


## kitchen ~:room:Kitchen
	The kitchen is fairly large with multiple [:%stoves:] and an industrial [:%refrigerator:], along with [:%cabinets:] mounted above the stoves and [:%sink:]. The kitchen looks like it's fit for a restaurant. In the back corner of the kitchen, there is a door{:%.dining_room.ki||%.kitchen.dr:: leading to the dining room:}.

	{:%.vanessa.intro_talk::[:%vanessa.intro @: Vanessa:] stands::There's a [:%vanessa.intro @: young woman:] in a maid outfit standing:} in front of the sink as she washes dishes. ::!%.daemon.intro_talk

	+ {dh} [Enter the hallway.]
		>> met_vanessa :: !%.kitchen.met_vanessa && %.vanessa.intro_talk
		>> #east_hallway

	+ {dr} [Enter the {:%.dining_room.ki||%.kitchen.dr::dining room::door in the back:}.]
		>> met_vanessa :: !%.kitchen.met_vanessa && %.vanessa.intro_talk
		>> #dining_room

	= stoves
	The stoves each have four burners and a large oven. The stoves are placed side by side for convenience. You could probably cook for an army with these.
	>> go.back

	= refrigerator
	The refrigerator has two large doors taller than you are. Its design is stainless steel and consists mostly of squared corners. Its utility-focused design is a sharp contrast to the warmth of the rest of the kitchen.
	+ [Open the fridge.]
		~~ if (!%.daemon.intro_talk)
			You take a step towards the fridge and {:%.vanessa.intro_talk::Vanessa::the woman:} interrupts you. "Can I get you something, Master?"
			{:!%.vanessa.intro_talk::<i>Master?</i>:}
			>> go.back
		-- You peer into the fridge. It looks even larger on the inside and is stocked full of an entire produce department, it seems, along with dairy and lunch meat and the like.
		>> go.back

	+ {fridge_back} [Go back.];; << RETURN

	= cabinets
	The cabinets look like cabinets in any other home, with the exception that there are many more of them.
	+ [Check the cabinets.]
		~~ if (!%.daemon.intro_talk)
			As you attempt to open a cabinet, {:%.vanessa.intro_talk::Vanessa::the woman:} speaks up. "You don't need to worry yourself with that, Master. Everything in the kitchen is under control." She gives you a polite smile that seems to carry with it a hint of warning.
			>> go.back
		-- There are so many cabinets that you don't even know where to begin. Opening random doors reveals dinnerware, boxes of crackers, potato chips and other snacks, cooking seasonings, and various other food items.
		>> go.back
	+ {cabinet_back} [Go back.];; << RETURN

	= sink
	The kitchen houses a large double-basin sink, both of which are elbow deep.
	<> {:%.vanessa.intro_talk::Vanessa::The woman:} is currently making use of the sink as she washes dishes. ::!%.daemon.intro_talk
	>> go.back

	= met_vanessa
	(:clearTitle:)
	Once you close the kitchen door, Erin grabs your arm and pulls you aside. "$:name! What have we gotten ourselves into!" She scans the area left and right, then looks up at you, whispering, "She was a monster!"

	+ "Our father was dealing with this alone."
	- "Well.. yeah.. okay, that's true. But monsters.. wow.." She sighs and resigns herself. "Let's go meet some more, I guess."
	+ [Continue];; <<


## dining_room ~:room:Dining Room
	The obvious attraction of the dining room is the lengthy dining [:%table:] lined with a dozen [:%chairs:]. Various portrait [:%paintings:] decorate the walls. In the back corner of the room is a door{:%.dining_room.ki||%.kitchen.dr:: leading to the kitchen:}.

	+ {hw} [Enter the hallway.];; >> #east_hallway
	+ {ki} [Enter the {:%.dining_room.ki||%.kitchen.dr::kitchen::door in the back:}.];; >> #kitchen

	= table
	(:bookmark "table":)
	The table and its row of [:%chairs:] stretch out for nearly the length of the room. On the table is a bowl of [:%fruit:] and a few [:%candelabras:].
	>> go.return

	= chairs
	The chairs are wooden with ornate designs carved into its legs and frame. They look sturdy enough to hold quite a bit of weight.
	>> go.back

	= fruit
	The bowl of fruit looks very appealing. Surprisingly, it appears to be real fruit.
	>> go.back

	= candelabras
	The candelabras' three golden arms hold white candles that seemed to have been used at least once already.
	>> go.back

	= paintings
	Each painting is a portrait of man or woman from long ago. Could these be your ancestors?
	>> go.back


## bar ~:room:Bar
	The bar isn't very large. It's more like a cozy drinking nook. There is a [:%counter:] lined with bar [:%stools:], and an array of various [:%drinks @: wines, liquors, and sodas:] behind the counter. It's designed to be serviced by a bartender, but you suspect it's currently self-service. Against the back wall is a classic [:%jukebox:].

	{:%.daemon.intro_talk::[:%daemon.intro @: Daemon:] is sitting::[:%daemon.intro @: Erin and a gruff looking man:] are sitting on stools:} the bar. :: %.sera.intro

	+ {hw} [Enter the hallway.];; >> #east_hallway

	= counter
	(:bookmark "counter":)
	The small counter with its marble counter top is tall and long enough to fit four bar [:%stools:].
	>> go.return

	= stools
	Each stool is about waist high and topped with leather covered foam padding.
	>> go.back

	= drinks
	The shelves behind the counter hold all manner of hard liquor and import wines. A glass-doored cooler off to the side holds beer and soda.
	>> go.back

	= jukebox
	The jukebox is something you would have expected to see in a public bar generations ago. It's made primarily of wood, with its face illuminated by an arch of colorfully lit tubing. It appears to fully loaded with vinyl records.
	>> go.back


## living_room ~:room:Living Room
	The living room is quite large, yet has a very cozy feel about it. At the end of the room is a gigantic widescreen [:%TV:] with a large [:%sofa:] facing it. To the side is a standing [:%cooler:] with soft drinks and water bottles, and a [:%table:] full of various snacks and fruits.

	~ if (%.vanessa.intro_talk)
		>> finola.name
		Sitting on the back of the couch is _:finola. :: %.vanessa.intro_talk

	+ {hw} [Enter the hallway.];; >> #east_hallway

	= TV
	The massive television is mounted to the wall facing the sofa.
	<> The screen is currently displaying some sort of sci-fi show with impressive special effects. :: %.vanessa.intro_talk
	
	~ if (%.finola.intro)
		>> finola.name
		<> _:finola is watching the show intently. :: %.finola.intro
	- >> go.back

	= sofa
	The sofa is set to face the television and looks like it could easily fit four or five people. It appears to be a similar model as the ones in the foyer.
	
	~ if (%.vanessa.intro_talk)
		>> finola.name
		[:= sadako.cap(_.finola):] is perched on the back of the sofa.
	- >> go.back

	= cooler
	The cooler is something simple like you'd find in a convenience store. It has a single glass door, behind which are many brands of sodas, juices, and bottles of water.
	>> go.back

	= table
	The large circular table is surrounded by many chairs. At the center of the table is a collection of snacks and fruits, probably for snacking while watching TV.
	>> go.back


/* === Back Hallway === */

## back_hallway ~:room:Back Hallway
	The back hallway connects to another hall on either end. <>

	[:&
		var rooms = sadako.find(["veranda", "theater","pool"], function(x) {
			return sadako.page_seen[x];
		});
		~~="There are ";
		if (!rooms.length) ~~+" three doors ";
		else {
			~~+"doors leading to the ";
			if (rooms.length === 1) ~~+rooms[0] + " and two other rooms ";
			else if (rooms.length === 2)  ~~+rooms[0] + ", the " + rooms[1] + ", and one other room ";
			else ~~+"theater, the pool, and the veranda ";
		}
		~~+"in this hallway."
	:]

	+ {dh} [Enter the east hallway.];; >> #east_hallway
	+ {ki} [Enter the {:#.theater::theater::first door:}.];; >> #theater
	+ {dr} [Enter the {:#.pool::pool::second door:}.];; >> #pool
	+ {bar} [Enter the {:#.veranda::veranda::third door:}.];; >> #veranda


## theater ~:room:Theater
	This is a very cozy movie theater. It only has about a dozen [:%seats:], and the [:%screen:] isn't as large as your typical theater, but it's the perfect size for this one.

	+ {hw} [Enter the back hallway.];; >> #back_hallway

	= seats
	The seats are of much higher quality than what you would see in an actual theater. They're almost like recliners without arms.
	>> go.back

	= screen
	The screen is much larger than a television, as would be expected. It's comprised of the same canvas as a movie theater screen and is intended to be used with a projector.
	>> go.back


## pool ~:room:Pool
	The indoor [:%pool:] spans the entire length of this room, save for a small walk-way and enough floor space for a few [:%benches:]. Off to the side is a fairly large [:%hot_tub @: hot tub:]. The entire exterior wall is made of ceiling-high [:%windows:] that look out onto the grounds of the estate.

	[:%seoyun.pool @: Seoyun:] is leisurely swimming in the pool. :: %.sera.intro

	+ {hw} [Enter the back hallway.];; >> #back_hallway

	= pool
	The pool's faint blue water looks warm and inviting.
	[:%seoyun.pool @: Seoyun:] is currently using the pool. :: %.sera.intro

	>> go.back

	= benches
	The wooden benches and sun loungers look very comfortable with their gentle sloping backs and arms.
	>> go.back

	= hot_tub
	The oval hot tub looks like easily fit six people side by side. It looks very cozy.
	>> go.back

	= windows
	The huge windows provide a clear view of the estate grounds while warming the pool area with sunlight.
	>> go.back


## veranda ~:room:Veranda
	The enclosed veranda is very cozy. The length of it is lined with [:%windows:] that provide a clear view of the large yard and the landscape beyond. There is a comfortable padded [:%bench:] here, along with a small [:%table:] and [:%chairs:]. Doors lead out to the back yard and into the back hallway.

	+ {hw} [Enter the back hallway.];; >> #back_hallway
	+ {yard} [Enter the back yard.];; >> #back_yard

	= windows
	The wood-framed windows warm the veranda with the sun rays cascading through. Through them you can see the well kept yard and the forest beyond.
	In the distance you can see what appears to be a woman running laps in the yard. :: %.vanessa.intro_talk && !%.seoyun.intro_talk
	>> go.back

	= bench
	The wooden bench could comfortably seat four people and has plush padding on the seat and back.
	>> go.back

	= table
	(:bookmark "table":)
	The small circle glass table sits just off to the side. There's a flower in a vase placed directly in the center. The table is surrounded by four [:%chairs:].
	>> go.return

	= chairs
	The chairs are made metal painted black. The backs of the chairs have an intricate design of roses and entangled vines.
	>> go.back


## back_yard ~:room:Back Yard
	The yard behind the [:%mansion:] is huge. It's acres of well-kept [:%lawn:], with only a [:%gazebo:] and a small [:%pond:] to break up the expanse of green.

	There appears to be a [:%seoyun.intro @: young woman:] jogging in laps around the yard. :: %.vanessa.intro_talk && !%.seoyun.intro_talk

	+ {ver} [Enter the veranda.];; >> #veranda

	= mansion
	It's very easy to appreciate the size of the mansion now that you are able to hold all of it in your sight.
	The building consists of three stories. The first two floors combined are wider than they are tall, and the third floor is concentrated at the center of the building. Off from the third floor you can see the railing of the patio. The roof of the mansion is covered in gray shingles and comes to pointed tips at its higher portions. At the highest point a gargoyle statue glares down at you.
	>> go.back

	= lawn
	[:&
		if (%.vanessa.intro_talk) {
			_.gardener = "Vanessa";
			_.their = "her";
			_.them = "her";
		}
		else {
			_.gardener = "The gardener";
			_.their = "their";
			_.them = "them";
		}
	:]
	The lawn has been kept to a pleasant length. It's just deep enough to gently cushion each footstep. Overall, the yard is quite massive, so it's impressive to see that it's been so well maintained. _:gardener really has _:their work cut out for _:them.
	>> go.back

	= gazebo
	The gazebo is a small, octagonal, roofed enclosure made of white painted wood, equipped with a bench made for two.
	>> go.back

	= pond
	The pond's water is impressively clear. Floating on the pond are a few water lilies.
	>> go.back


/* === Second Floor Hallway === */

## 2F_hallway ~:room:Second Floor Hallway
	This section of the second floor consists of only a hallway leading to each of the resident's bedrooms. The end of this hallway wraps around the corner to the back hallway, and the start leads down a set of stairs to the lobby.
	On either side of the hallway are doors to each room. On each door is a little sign with the person's name. On the left is Seoyun's room and [:%erins_room @: Erin's room:], and to the right is Sera's room and your room.
	
	+ {down} [Descend the stairs to the foyer.];; >> #foyer
	
	= erins_room
	The door to Erin's room is white and plain like all of the rest. On the door hangs a sign that reads her name in neatly written letters.
	
	As you step up to the door, Erin asks, "Did you want to go in? You can if you want." :: $.erin_following
	
	+ {erin_enter} [Enter the room.] :: $.erin_following;; >> #erins_room
	+ [Knock on the door.] :: !$.erin_following
		You knock on the door but there isn't any answer. She must not be in her room right now.
		>> go.back
	+ {erin_back} [Go back.];; << RETURN


## erins_room ~:room:Erin's Room
	Her room is a decent size. At the moment it's quite bare since the two of you have just moved in. However, it's still furnished with a [:%bed:], a [:%dresser:], a small [:%desk:], and a [:%television:] on top of the dresser. There is also a [:%window:] from which you can see the treetops of the surrounding forest.
	
	Erin's [:%foyer.luggage @: luggage:] is neatly collected into the center of the room. :: %.daemon.intro_talk

	{erin_comment} Erin looks around the room. "Hey, this isn't too bad." :: $.erin_following && !%.erins_room.erin_comment
	
	+ {hw} [Enter the hallway.];; >> #2F_hallway
	
	= bed
	Erin's full-sized bed is covered with neatly tucked-in pink sheets. Pink is her favorite color, of course. Were the sheets chosen for her knowing that, or was it just a coincidence?
	>> go.back
	
	= dresser
	Her dresser is squat and has four drawers. The top of the dresser is quite long and could easily be used as a tabletop.
	>> go.back
	
	= desk
	The desk is not that big and doesn't have any drawers, but it is still quite usable for any writing tasks that may need to be done.
	>> go.back
	
	= television
	Her television is a mid-sized flatscreen, which makes it about the same as the one she had back home, but it is of a higher quality brand. 
	>> go.back
	
	= window
	From the window you can see the forest surrounding the mansion. The forest seems dense and impenetrable. It makes you wonder whether the forest is secluding the mansion or defending it.
	>> go.back

/* === Items === */

## master_keys
	Your father left a set of keys on the desk. You assume they're for the various rooms in the mansion.

	+ {take} [Take the keys.]
		You attach the ring of keys to the belt loop of your jeans.
		<div style="height:2em"></div>
		You can access your inventory using the <img title="inventory" class="inline-icon" src="icons/inventory.png"> icon at the top left of the screen. ~:class:info
		(:move "items", "master_keys":)
		>> go.return

	+ {dont_take} [Go back.];; >> $:bookmark

	= inventory
	They're a large collection of keys for the many rooms in the mansion.

	+ [Use a key.]
		>> office.cabinet_unlocked :: $.bookmark === "%office.cabinets" && !%.office.cabinet_unlocked
		You can't find a use for the keys here.
		>> go.inventory
	+ [Back];; >> #inventory


/* === Characters === */

## walter
	= notebook
	Walter $:lname is your father. He's a very gruff and demanding invidual. 
	You've never been on the best of terms with your father, even when he was around. But then about seven years ago, he left. There was no divorce. Neither your father nor your mother requested one. In fact, throughout the years, your mother acted as though she expected your father to return, and so you believed it as well, until you didn't. You expected to never see him again. But that all that changed with his phone call.
	It's a mystery to you how your father came to acquire this mansion or what business he has in another country. Or even which country that is. :: #.office
	<> But more importantly, since when did he first learn of myths? :: %.kitchen.met_vanessa
	<< END


## erin
	= notebook
	Erin is your sister. She is younger than you by only a couple years. She is cute and animated like a cartoon character. Sometimes she's a bit loud and blurts out things before speaking, but that just adds to her charm.
	You and Erin have been living together along with your mother in the city your whole lives. Your father left the three of you when you were about fifteen years old. After graduation, both you and your sister got jobs to help support your mother. Life was pretty uneventful until your father called you out of nowhere and asked you to help him with his business. Neither of you are sure why you felt the need to honor his request.
	<< END

	= intro
	Erin is squirms in her seat, nervously tapping her foot. Her eyes are still fixed on the office door, shaking her head in disbelief.
	>> talk_menu

	~ if (%.erin.intro_talk)
		"Come on. We should start looking for {:%.erin.intro_vanessa::Vanessa::the tenants:}."
		>> go.eoc

	= intro_talk
	Erin looks at you with a frantic expression. "$:name! What are we going to do?"

	+ {intro_vanessa} "Maybe Vanessa will know what to do?"
		Erin groans. "Dad didn't even tell us who she was." She scratches her scalp in aggravation. After a sigh she says, "But, yeah.. We should probably try to find her."
	+ "Manage the estate, I guess."
		"Well, yeah. But isn't this a bit much? I know you've had management experience, but this place is ginormous and we don't know anything about it. We haven't even met the tenants yet!"
	-
	+ "Then let's go do that."
	- "Right. Let's do this."
	>> go.eoc

	= following
	~ if ($.room === "office")
		~~ if (%.erin.following_office_father)
			Erin flicks some papers on the desk while mumbling to herself. She doesn't seem to be in the mood to talk.
			>> go.back
		--  Erin is idly playing with things on your father's desk.
		>> talk_menu
		== following_office_father
		Erin just shakes her head. "Dad is so uptight. Everything is so neat and tidy." Her eyes narrow. "I just want to mess it all up."

		++ "Do you dislike our father?"[] you ask.
			"Dislike?" She tilts her head in thought. "No, not dislike. Just.. He left us, ya know? I'm not sure I can forgive him for that." She scrunches her face into a scowl and says, "And <i>this</i> situation isn't helping."
			>> go.eoc
	~ else if ($.room === "foyer")
		~~ if (%.daemon.intro_talk)
			Erin cocks her head. "Huh. Our luggage is gone. Did Vanessa take it?"
			>> go.back
	 	~~ else if (!#.east_hallway)
			Erin keeps staring nervously at the hall on the right where the flying object disappeared.
			>> talk_menu
			Erin frowns. "I wonder what it was. Do you think it'll come back? I really hope not.."
			>> go.eoc
	~ else if ($.room === "kitchen")
		Erin glances around the kitchen. She's always been a reliable cook at home, so she's probably appreciating the supplies and equipment provided here.
		~~ if (!%.daemon.intro_talk)
			>> talk_menu
			~~~ if (%.vanessa.intro_talk)
				"She seems.. umm.. nice?" Erin says without any confidence.
			~~~ else
				"Who is that?" she whispers to you. "Do you think <i>she's</i> Vanessa?"
			--- >> go.eoc
		-- >> go.back
	~ else if ($.room === "veranda")
		Erin is pressed up against one of the window panes, gazing out into the back yard with a look of awe on her face.
		>> talk_menu
		"This place is amazing! Look at this view!" She performs a little twirl. "This veranda is also really warm and cozy. It feels so good."
		++ "I'm glad you like it."
			She  nods. "This place may not be that bad after all. It's definitely much better than back home."
			>> go.eoc
	~ else if ($.room === "back_yard")
		Erin is smiling, clearly enjoying the scenery and being outdoors.
		~~ if (%.erin.seoyun_1a && %.erin.seoyun_1b)
			<> It seems like a shame to keep disturbing her while she's appreciating nature.
			>> go.back
		-- >> talk_menu
		~~ if (%.seoyun.intro_talk)
			~~~ if (%.erin.seoyun_1a || %.erin.seoyun_1b)
				"I think Seoyun likes you, you know." Your sister beams at you. "Do you like her too?"
			~~~ else
				"Wow, Seoyun is so pretty, and also really nice. You should get to know her better," she lightly elbows you, "if you know what I mean."
			*** {seoyun_1a} "You're okay if your brother dates a monster?"
				"Yeah. Well, no. Well... I don't know. She seems okay. Maybe they're not all that bad." She seems confused.
			*** {seoyun_1b} "But aren't kumihos known for killing their men?"
				"Mmm.. But those are just stories, right?" She ponders. "Although, I guess myths are based on the stories.. or vice versa." Erin sighs. "This hurts my head."
		~~ else if (%.vanessa.intro_talk)
		 	"Look, $:name. Another myth," Erin says, nodding in the direction of the woman running laps. "She's so pretty too. And her tails look so fluffy! Do you think she'd let me pet them?"
		~~ else
			"This place is amazing!" She twirls in a little circle. "This is so much better than being stuck in the city back home. I'm really going to enjoy this place."
		-- >> go.eoc
	~ else if ($.room === "living_room")
		~~ if (%.finola.intro_talk)
			~~~ if (%.erin.finola_grumpy)
				Erin is still staring at Finola. She appears to be pondering something. You feel it's best to not get pulled into whatever she's planning.
				>> go.back
			---
			Erin looks like her eyes could bore holes in the back of Finola's head. She appears to be incredibly grumpy
			>> talk_menu
			"I always wanted to meet a fairy. Why does ours have to be so rude?" she asks with a frown.
			+++ {finola_grumpy}"Maybe she'll warm up to you."
				"Maybe... I <i>am</i> pretty likable." She grins.
				>> go.eoc
		~~ else if (%.finola.intro)
			Erin looks at the minature girl with curiousity and apprehension.
			>> talk_menu
			"A small girl with butterfly wings...," she ponders. "Does that make her a pixie? a fairy? I always get them confused."
			>> go.eoc
		~~ else if (%.vanessa.intro_talk)
			Erin's attention is focused entirely on the creature sitting on top of the sofa. "What <i>is</i> that?"
			>> go.back
	~ else if ($.room === "bar")
		~~ if (%.daemon.intro_talk)
			Erin keeps glancing over at Daemon who is still sipping away at his drink.
			~~~ if (%.erin.daemon_intro)
				<> When she catches you watching her, she blushes and turns her back to you.
				>> go.back
			--- >> talk_menu
			{daemon_intro} "So, uh.. He's not really what I expected the bogeyman to look like. He's actually kinda..." She glances at you then down to the floor. "Nevermind."
			+++ "Are you into him or something?"
				"What? No! It's just... Just be quiet." She pouts.
				>> go.eoc
	~ else if ($.room === "pool")
		~~ if (%.daemon.intro_talk)
			Erin seems to be mesmerized by Seoyun doing laps in the pool.
			~~~ if (%.erin.pool_watching_seoyun)
				<> It seems difficult to draw her attention away.
				>> go.back
			--- >> talk_menu
			{pool_watching_seoyun} "I'm a bit jealous," she says. "Seoyun really has an amazing body."
			+++ "Are all myths attractive like this?"
				She thinks for a bit. "I have no idea. But
			+++ "You shouldn't be jealous. You're also nice looking."
				"Aw, thanks." She smiles. "But still.. 
			--- <> even Sera--as much as she freaked me out--was cute as hell. Her top half, I mean, of course. And Daemon is kind of hot, even though he's a bogeyman, I guess...?"
			+++ "It's kind of intimidating."
				She laughs. "Surrounded by hot women? Must be rough! Poor baby!"
				++++ "Ugh. Stop."
			+++ "It's no big deal."
				"Oh?" She elbows you. "Are you too cool for school? Or do you just think you're all that? My big stud of a brother."
				++++ "Stop it. That's weird."
			---	She laughs. "I'm just picking, you weirdo."
			>> go.eoc
	~ else if ($.room === "erins_room")
		Erin is calmly evaluating her surroundings, since she will be living in this room for the foreseeable future.
		~~ if (%.erin.her_room_talk && !%.daemon.intro_talk)
			<> She seems to be lost in contemplation.
			>> go.back
		-- >> talk_menu
		~~ if (!%.erin.her_room_talk)
			{her_room_talk} "This room isn't so bad, right? It's actually a lot better than my room back home. Yours too."
		
			+++ "Yeah."
				"Yeah.. This could work." 
				>> go.eoc :: !%.daemon.intro_talk
				<> She nods and then turns her attention to <>
		~~ else
			Erin looks at <>
		-- her luggage. "I guess Vanessa really did carry this all up on her own in just the short time we were with Daemon. She's really amazing."
		(:endTask "main_3.unpack":)

		++ "What do you think about her?"
		-- "Vanessa?" she asks. "She seems nice. Or at least pretends to be nice. She seems kind of.. off though. Like, I'm not sure if I should trust her. Or trust any of them."

		++ "I also got that impression."
			"Yeah. Do <>
		++ "Aren't you overreacting?"
			"Overreacting? They're immortal creatures from legend that have a history of killing people. I think my reaction is quite normal." She shakes her head. "But I wonder, do <>
		-- you think Dad trusted them? I mean, Vanessa has been with the family for generations, I guess, so she must be at least a little trustworthy. But the others...? We're sharing a roof with monsters now, $:name. <i>Monsters</i>. How the hell did this happen?"

		++ "What do you think we should do?"
			Erin tilts her head as she thinks. "Well.. we can't leave. I mean, we could, but Dad left this place in our care. {:%.office.erin_deed::He even gave us ownership. :}We can't just abandon it..."
		++ "We can't just leave."
			Erin sighs. "I know. They might be monsters but I can't just abandon them like that.{:%.office.erin_deed:: Besides, we own this place now...:}"
		-- Your sister paces around the room. "But at the same time, this is a whole new world, ya know? Myths are real? <i>All</i> of them? Werewolves? Vampires? Bigfoot? The slit-mouthed woman? Slender Man? <i>Everything</i>? Seriously, that's really scary."

		++ "The last one was created on a forum."
		-- "Does it matter? Vanessa said that she's not sure which came first. If myths are based on the urban legends instead of vice versa, that means it wouldn't matter if you could track the origin of the myth. All that matters is that it exists." She stops pacing. "Do you think it's based on how widely the legend has been spread? I wonder how it works..."

		++ "It sounds like you're getting used to this."
		-- "Yeah. Well, no. I just think it's interesting. It doesn't mean I'm not scared out of my mind at the idea of it." Then she sighs. "But I guess there's not much we can do about it. Either we abandon this place and let it go to hell, or we do what our dad asked and take care of this place while waiting for him to return, if ever."

		++ "He never did say he was returning, did he."
			"No. No, he didn't," she grumbles.
		++ "He'll be back."
			She scoffs. "Yeah. Just like he did seven years ago. Oh wait! He didn't at all."
		-- Just then there is a gentle knock at the door. Erin gives you a confused look before she walks over to the door and opens it. Behind it is Vanessa.
		"Excuse me, Mistress, Master, but I request your assistance in preparing for the new tenants."
		Erin shrugs. "Welp. Let's go do this then," she says to you, apparently having made her decision.

		++ "Okay. Let's go."
		-- The three of you leave the room and Vanessa leads you to the back hallway. It hasn't even been a day but you've had enough surprises to last a year. What other surprises are in store for you and your sister?
		<div style="height:2em"></div>
		This is the end of the demo. Thank you for playing! ~:class:info notebook
		<div style="height:2em"></div>
		<center><h1>MONSTER</h1></center> ~:delay:3500
		<< END
	- She doesn't appear to have anything to say right now.
	>> go.back

	= talk_menu
	+ [Talk to Erin.];; <<
	+ {dont_talk} [Go back.];; >> $:bookmark


## vanessa
	= notebook
	Vanessa is a maid working for your family. She is a shoggoth--whatever that is--that used to work for the Elder Ones or whoever before she came to work for your family. Apparently she has been serving them for 100 years. It sounds as though she is also really strong. You don't know much else about her race or history.
	Vanessa seems to be very polite and kind. Or at least that's the appearance she displays. It feels as though there may be something just below the surface. She may not be quite as obedient as she leads you to believe.
	
	<< END

	= intro
	The woman smiles at you as you approach. Her complexion is pale with a slight blue hue. Her hair is an odd shape, like she used too much hair gel, and it looks wet to the touch. Her skin also has a bit of a moist sheen to it.
	>> talk_menu

	~ if (%.vanessa.intro_talk)
		Vanessa glances up from her dishes. "If you are still looking for the others, Master, I believe I saw one of them in the living room and another in the back yard. The living room is the last door in this hall, and you can access the back yard via the veranda in the back hallway."
		>> go.eoc

	= intro_talk
	(:endTask "main_1.find_vanessa":)
	She bows. "Master $:name, I presume," she says in a gentle voice. She turns her attention to your sister. "And you must be Mistress Erin. Welcome. I am Vanessa, your humble servant."

	+ "You know about us?"
	- "Oh yes, Master. Master Walter has discussed the two of you many times. I was also informed that you would be arriving today to take his position."
	"Temporarily," Erin adds.
	Vanessa just smiles.

	+ "Is it just us here, Vanessa?"
	- "Oh, no, Master. There are four others besides myself, with two more on the way."
	That catches Erin's attention. "What? He dumped this on us knowing that there were more people coming? We don't even know what we're doing!"
	"Master Walter has complete faith in you, and I am here to assist you in any way that I can."
	Erin grumbles.

	+ "What can you tell us about the tenants?"
		Vanessa looks at you with a blank expression for an uncomfortable amount of time. "I assume that Master Walter has told you nothing?"
		"Literally nothing," Erin growls.
		She begins, "The residents here are not.. I am not.. like you."
		"What.. do you mean by that?" your sister practically whispers.
		"Have you not noticed anything peculiar about my appearance?"

		++ {intro_skin} "Your skin?"
		++ "Your hair?"
		-- Vanessa lets loose an uncharacteristically loud laugh. It subsides into a volley of giggles that carries on for quite some time. Erin looks on with a concerned expression. Eventually the maid regains her composure.
		"Yes, yes," she smiles to herself, "{:%.vanessa.intro_skin::my skin. My skin::my hair. My hair:} is definitely a factor. But I was referring more to <i>this</i>." She lifts her skirt to her knees--or rather, she lifts her skirt to where her knees <i>should</i> be. There are no legs beneath that skirt. Instead there is only a collection of thick and slimy tentacles that merge into a trunk-like base with smaller tentacles forming a root system where her feet should be. They writhe and squirm and make squishy sounds while they do.
		Erin staggers back and grips your arm. "<i>What in the holy %@#$!</i>"
		Before you can say or do anything, Vanessa says, "Please, don't be alarmed. I know it must be shocking, but I swear my servitude to you. I would never harm you. I would defend you with my life." She lowers her skirt again, flattening it out with the palms of her hands, hiding the tentacles the best that she can.

		++ "I don't think I'll need you to defend me with your life."
		-- "You may be surprised, Master."
		"I'm already surprised!" Erin shouts. "What <i>are</i> you!"
		Vanessa gives Erin an empty smile. "I am a shoggoth, Mistress. Long ago, our race was created by The Elder Ones whom we served as mindless and formless slave builders. However, we evolved, learned to think for ourselves, and broke free from of our creator's control. We are still servants by nature, so we eventually swear our servitude to ones that we find worthy. This is I did with your family, many, many generations ago."

		== intro_loop_1
		** {intro_loop_1a} "How long have you been working for my father?"
			"I've only been serving Master Walter for just under a decade. I've been serving your family for just over a century," Vanessa says matter-of-factly.
			Erin blinks. "A <i>century</i>?"
			"Indeed, Mistress. I served your grandfather and grandmother, and your great-grandfather and great-grandmother as well."
		** {intro_loop_1b} "Why did you start serving my family?"
			"A bit of fortune allowed me to witness the event of your great-grandparents risking their lives to defend a pair of myths. They were only fragile humans, but were willing to risk it all for a pair of 'monsters' that they had never seen before. When the battle was over, I swore my servitude to them on the spot."
		** {intro_loop_1c} "The others are like you?"
			"Yes, and no. They are myths, but they are not shoggoths."
			"Myths?" Erin asks.
			"That is the name you humans have given us throughout the years, Mistress. You believe myths to be works of fiction, but they are not. They are as real as I am."
			"... I see.." Erin is clearly considering the implications of her statement.
		** ;; >> intro_loop_2
		-- >> intro_loop_1

	= intro_loop_2
	* {intro_loop_2a} "Can you tell me more about myths?"
		"Myths have existed since mankind was able to communicate thoughts. To be honest, no one is entirely sure whether myths were born of spoken legend, or spoken legends were born of myths. There appears to be a direct relation to one another in either case. If a well known historical myth or urban legend exists, there's a good chance that there is in fact a myth out there that it was based on--or is based on it, as may be the case."
		Erin shakes her head. "That's confusing."

	* {intro_loop_2b} "Do myths fight a lot?"
		Vanessa gives a subtle shrug. "No more than any other species, I suppose. Where there is interaction, there is conflict. However, myth conflicts tend to escalate to a level beyond anything a human could imagine."

		++ "How big are we talking?"
			She smiles. "Quite big, Master. For example, I could level this entire mansion in a matter of seconds if I so wished. There are many myths much more powerful than I."
			"... wow...," Erin whispers in awe.
			"Indeed. Many natural disasters are caused by the squabbles of myths. Norse and Greek mythologies were accurate in this fact."

	* {intro_loop_2c} "Do you know what we should be doing?"
		"I would suggest introducing yourself to the tenants, Master. Surely they would like to meet the persons that will be managing this establishment."
		Erin nods. "Makes sense."
		Vanessa continues, "Soon, I will also require your assistance in regards to the two upcoming arrivals."

		++ "Okay. Just let us know when you need our help."
			"Thank you. I will, Master." Vanessa bows her head.

	* ;; >> intro_loop_2_end
	- >> intro_loop_2

	= intro_loop_2_end
	+ "We should probably introduce ourselves to the other tenants now."
	(:setCharInfo "vanessa", "Vanessa":)
	- "I understand, Master. I must return to my cleaning as well. Please let me know if you need anything else."
	(:addTask "main_2.find_tenants":)
	>> go.eoc

	= talk_menu
	+ [Talk to {:%.vanessa.intro_talk::Vanessa::the woman:}.];; <<
	+ [Go back.];; >> $:bookmark


## seoyun
	= notebook
	Seoyun is a very attractive young woman who is a also kumiho. A kumiho seems to be a fox spirit of some sort? Seoyun at least sports some cute ears and nine bushy tails. She seems to enjoy being outdoors and athletic, and has a physique that expresses that.
	Seoyun has beautiful Korean facial features, but her voice does not carry any accent. Perhaps she hasn't been in Korea for a long time, if ever? You don't know anything of Seoyun's past.
	<< END

	= intro
	As the woman completes a lap and draws closer, you can see that she is also a myth. Amidst her long flowing auburn hair is a pair of pointed animal ears perched atop her head. Trailing behind her is a collection of large and puffy auburn furred tails with white tips. You are unsure of the number of tails, but you can count at least five.
	>> talk_menu

	= intro_talk
	
	"Oh, hi," she says, jogging in place, "You must be the new landlords?"

	+ "That's right."
	- "Hi. I'm Erin. This is my brother, $:name," Erin says, extending a hand. The woman shakes it while still trotting in place.
	"I'm Seoyun. Nice to meet you."
	"Seoyun? That's a pretty name."
	"Thank you. It's Korean," she says with a smile.
	(:addTask "main_2.find_seoyun", "complete":)

	= intro_loop_1
	* {intro_loop_1a} "You're a myth too?"
		Seoyun giggles. "What gave it away?" She stops jogging and begins stretching, her toned body straining against her jogging outfit. "I'm a kumiho. A nine-tailed fox spirit."
		Erin gasps. "I think I've seen those in movies. Aren't they.. um.. evil?"
		Seoyun grins, her sharp canines glint in the sunlight. "They can be."

		== intro_loop_2
		** {intro_loop_2a} "Why would we let evil myths stay here?"
			"Define 'evil'." She shrugs. "Think about the myths that you've heard about. How many were evil for evil's sake? Most myths are just focused on self-preservation, but humans consider it evil when it conflicts with their own self-preservation."
		** {intro_loop_2b} "Are we in danger then?"
			"Not likely. This housing that you provide helps aid our self-preservation. If anything, you are probably safer now than you've ever been."
			"$:name! We have our own body guards now!" Erin says.
			Seoyun chuckles. "Something like that."
		** {intro_loop_2c} "Should we trust you?"
			She shrugs. "That's up to you. I won't try to convince you."
			"Dad let her stay here. He must have trusted her, so it must be okay," Erin interjects.
			"Then I guess the question that remains is whether you trust your father."
			"Urgh," Erin groans.
		** ;; >> intro_loop_1
		-- >> intro_loop_2

	* {intro_loop_1b} "Are you out here a lot?"
		Seoyun nods. "I like nature. It's really peaceful out here. It's just the sounds of the birds and insects, and the warmth of the sun on my skin feels amazing. I just can't stay cooped up in the mansion all day."
		"Yeah, I know what you mean. I also like being out in the sun like this," your sister says.

		++ "Huh? But you spend all of your time indoors on the PC."
			"It doesn't mean that I don't like to spend time outside! Sometimes!" Erin crosses her arms and makes a grumpy face.
			Seoyun giggles.
		++ [Say nothing.]
			You decide to not point out the contradiction. For whatever reason, it seems as though Erin wants to get into Seoyun's good graces. She gives you a thankful look, knowing full well that she left herself wide open to attack.

	* {intro_loop_1c} "Can you tell us more about kumiho?" :: %.seoyun.intro_loop_1a
		"Yeah, they seem really cool from the stuff I've seen!" Erin grins.
		"Well, I don't know about <i>that</i>." She chuckles and shrugs. "There's not much to tell, though. I guess the biggest thing is that I can shapeshift into a human form."
		"Human? So then your original form is..."
		"That's right. A fox. I can shift into a complete human form, indistinguishable from a real human, if I tried. That takes a lot of energy though, so I'm usually in this half-assed form when there's no need to go all the way. Plus, my tails and ears are cute, right?" She winks.
		You feel the blood rush to your cheeks and Erin giggles at you.

	* ;; >> intro_loop_1_end
	- >> intro_loop_1

	= intro_loop_1_end
	+ "We're going to go talk to the other tenants now."
	- "That's cool. I'm going to hit the shower. You can catch me at the pool after if you need me."
	Seoyun waves and heads toward the building. From where you're standing, all you can see is a flowing mass of tails bouncing along the lawn and then disappearing through the doorway.
	(:setCharInfo "seoyun", "Seoyun":)
	>> go.eoc

	= pool
	Seoyun is casually swimming in the pool, just swimming back and forth without any real direction or goal. 
	<> She still has her fox ears, but you can see that she got rid of her tails. It must make it easier to swim that way. Swimming with nine bushy tails would probably be quite difficult. :: %.seoyun.pool === 1
	~ if (%.seoyun.pool_erin_back)
		<> You should let her do her laps like she asked.
		>> go.back
	~ if ($.erin_following)
		>> talk_menu
		== pool_erin_back
		~~ if (%.seoyun.pool_talk_later)
			"Hey! You found your sister." She says and gives Erin a little wave as she treads water.
			Erin smiles. "Yeah. I was talking with Deamon. He seems nice."
		~~ else
			"Hey guys." Seoyun says as she treads water. "I was just thinking about you, and.. Have you run into Daemon yet?"
			"Yeah. We were just talking. He seems pretty nice," your sister says.
		-- Seoyun frowns. "Be careful with that guy. He's a bogeyman, you know. He has a really, really dark side."
			Your sister sighs and pouts. "Fine.. I'll be careful."
		++ "Is he really that scary?"
		-- "He is literally the stuff of nightmares, so yeah. He can be pretty scary."
		++ "That doesn't sound like boyfriend material."
			Erin gasps. "He's not my boyfriend! I mean, I didn't want him as my boyfriend! I didn't even think of him that way!" Her face is as red as a tomato.
		++ "Thanks. We'll be careful."
			Seoyun nods. "No problem. I wouldn't want to the cute one to get hurt."
			Erin blushes.
		-- "Anyway," Seoyun says after a little giggle, "I still need to get my laps in, so I'll talk to you guys later."
		>> go.eoc
	~ if (%.seoyun.pool_talk_later)
		<> As much as you'd like to chat, you'll have plenty of time to talk to her after you find your sister.
		>> go.back
	- >> talk_menu
	"Hey, $:name," Seoyun calls out to you as she treads water. She looks around the pool area. "Where's your cutie of a sister?"
	+ "I'm trying to find her."
		She frowns. "Oh, I see. Sorry, I have no idea where she is. I came right here after my shower."
		++ {pool_talk_later} "Thanks anyway."
			"Yeah, no problem. I'll talk to you later when you're not busy."
			>> go.eoc

	= talk_menu
	+ [Talk to {:%.seoyun.intro_talk::Seoyun::the woman:}.];; <<
	+ {dont_talk} [Go back.];; >> $:bookmark


## finola
	= notebook
	Finola is a fairy. She's a bit blunt, to be honest. She's cute as a button with her miniature body and beautiful blue and black butterfly wings, but she's actually quite rude. She seems to rub Erin the wrong way, but Finola doesn't seem to have a problem with you.
	<< END

	= name
	[:&
		var link = "%finola.intro";
		if (%.finola.intro_talk) _.finola = sadako.writeLink("Finola", link);
		else _.finola = "a " + sadako.writeLink(((%.finola.intro) ? "miniature girl" : "small creature"), link) + " with butterfly wings";
	:]
	<<

	= intro
	~ if (%.finola.intro_talk)
		Finola is absorbed in her show and oblivious to the world.
		~~ if (%.sera.intro && !%.daemon.intro)
			>> talk_menu
			"Hey. Where's your sister?" Finola asks, finally noticing you standing beside her.
			+++ "I was hoping you had seen her."
			--- "Nope. Sorry. Have you checked next door? I know Daemon usually gets a drink around this time of day. Maybe he's keeping her company," she suggests. "Although, I kind of hope not."
			>> go.eoc
		-- <> You decide to leave her be.
		>> go.back

	- As you get closer, you realize that small creature is actually a little girl with what looks like a set of blue and black butterfly wings sprouting from her back. She is so little that she could easily fit into the palm of your hand. The girl appears to be watching TV and pays you no mind.
	Erin whispers. "Oh! I think she's the one that flew by us in the foyer." :: %.finola.intro === 1
	>> talk_menu

	= intro_talk
	"Excuse me. Hello?" Erin says.
	The little girl cranes her head back to look at you and your sister. "Yeah? What."
	Erin crosses her arms. "Wow. Rude."
	The girl just shrugs. "I'm trying to watch TV. My show just started."
	"What are you, like a fairy or something?" Erin asks, ignoring her complaint.
	"<i>Pin-pon.</i> Got it in one."
	"I thought fairies were supposed to be nice." Erin frowns.
	"You watch too many cartoons."

	+ "Anyway, I'm $:name, and this is Erin."
	- Giving up on being able to watch her show in peace, the girl turns around to face you. "Fionnuala. My friends call me Finola."
	Finola holds her hand out to you.
	(:addTask "main_2.find_finola", "complete":)

	+ [Shake her hand.]
	- You extend your hand for a shake. Your hand is practically the size of the the fairy. She firmly grasps just your index finger and gives it a good shake.
	"So," she starts after you break the handshake, "you're the new landlords, yeah?"
	"You've heard about us?" Erin asks.
	Finola smirks. "I'm a fly on the wall, so to speak. I hear lots of things."

	+ "What do you think about the other tenants then?"
	- "Oh? You want the dirt already?" Finola laughs. "That's fine, that's fine. Who do you want to know about?"

	= intro_loop_1
	* {intro_loop_1a} "Tell me about Vanessa."
		"Vanessa is super nice, but if you upset her, she'll show you what it's like to stare into the deepest depths of hell. I'm serious: don't do it. For the sake of your sanity, stay on her good side."

	* {intro_loop_1b} "Tell me about Seoyun." :: %.seoyun.intro_talk || %.finola.intro_loop_1e
		"Hmmph. Seoyun. We don't get along that great. She seems nice enough, but a bit uptight. She's always yelling at me and doesn't let me have any fun." She tilts her head and studies your face. "I bet you and Seoyun will get along though. She's super pretty, after all. And kumihos love their men."
		
	* {intro_loop_1c} "Tell me about Sera." :: %.finola.intro_loop_1e
		"Sera's the best!" Finola exclaims as her face brightens into a warm smile, something you didn't think she was capable of displaying. "She's kinda scary looking though, but don't hold that against her. You'll really like her once you get to know her."
	
	* {intro_loop_1d} "Tell me about Daemon." :: %.finola.intro_loop_1e
		"<i>That</i> guy," she chuckles. "Nah, Daemon is cool. Sorta. We're kinda like partners in crime, I guess. He doesn't take things too seriously, which is what I like. But he's dangerous, so you should be careful." She looks at Erin now. "Especially you. You're exactly his type. Watch yourself."
	
	*
		** {intro_loop_1e} "Who did I miss?"
			"There's still {:!%.seoyun.intro_talk::Seoyun, Sera,::Sera:} and Daemon," Finola replies in a flat tone.
			(:addTask "main_2.find_seoyun":) :: !%.seoyun.intro_talk
			(:addTask "main_2.find_sera":)
			(:addTask "main_2.find_daemon":)
			(:removeTask "main_2.find_tenants":)
			>> intro_loop_1
		-- >> intro_loop_1_end
	- >> intro_loop_1

	= intro_loop_1_end
	"So.. Is that everyone now?" Erin asks Finola.
	"Yep. You got them all."

	+ "Well, I guess we should go track down the others then."
	- "Have fun with that." Finola laughs as she turns her back to you and attempts to re-immerse herself in her television show.
	(:setCharInfo "finola", "Finola":)
	>> go.eoc

	= talk_menu
	+ [Talk to {:%.finola.intro_talk::Finola::the minature girl:}.];; <<
	+ {dont_talk} [Go back.];; >> $:bookmark


## sera
	= notebook
	Sera appears to be a spider girl. Her upper torso is that of a cute girl in her late teens. She has short black hair, matching black fingernails, and even her eyes are like black onyx orbs. From her waist down, her body is that of a giant spider. She has a large abdomen and long, spindly legs devoid of any hair.
	~ if (%.sera.intro_chase)
		Daemon says she's really nice. You don't know much about her beyond that.
	~ else
		Your first impression is that Sera is seems like a nice girl. People being frightened of her seems to be a bit of an emotional struggle for her.
	- << END

	= intro
	(:clearTitle:)
	As you enter the foyer, you hear the sounds of scurrying above you and the chandelier shakes, immediately followed by a large shadow darting across the ceiling.
	"$:name..?" Erin asks nervously. "What was--"
	She is stopped short by a large form dropping from the ceiling and landing only a few feet in front of you. It takes a moment for your brain to register what it's seeing.
	"What.. what.. what.. what.." Erin is hysterical.
	It's becoming clearer as to what you're staring at. A girl with short black hair wearing a bare midriff shirt is looking down at you. She towers above you, but she doesn't appear to be standing on anything. You begin to realize that below her midriff shirt, her human torso merges into an enormous insectoid body. From the body sprouts eight long legs.
	"She's a @#$!ing <i>spider</i>!" Erin screams. Having completely lost it, your sister turns on her heels and bolts down the east hallway, disappearing out of sight. You have been abandonded.

	[:& $.erin_following = false :]

	+ [Stand your ground.]
		You stare up at the girl. She bends down, leveling her face with yours, and says, "I'm so sorry!" She looks like she's about to cry. "I swear, I didn't know anyone was here!"

		++ "It's okay. We were just startled."
			She sniffles. "Thanks for being so kind. I don't get that much."
		++ [Say nothing.]
			You are a bit dumbfounded and continue to stare. She's actually sobbing now.
			"I'm always scaring off people." She looks at you with pleading eyes. "I know I look scary, but really, I'm not going to hurt you."
		--

		++ "Do you live here too?"
		-- She takes a few steps back and sits on the ground. Or at least what constitutes sitting, which is to say that she pulls her legs in close and rests her abdomen on the floor. She wipes the tears with the back of her arm, then looks at you with moist eyes and sniffles. "Yeah. I'm Sera. It's nice to meet you."
		(:endTask "main_2.find_sera":)

		++ "Hi Sera. I'm $:name."
		-- "Hi $:name," she replies softly, She takes a moment to think. "Why are you here? Are you friends of Walter?"

		++ "He's our father. He left us in charge while he's out of the country[."]," you explain.
		-- "He left the country!" she gasps. "Nobody ever tells me anything!"

		++ "It seems like everyone else knew."
			"Ugh. Of course they did." Sera sighs. "But anyway, that means you're the new landlords?"

		++ "We're just as surprised as you are."
			She frowns. "Did he abandon you? That wasn't very nice."
		--

		++ "Yeah. It's just me and my sister now. Speaking of which, I need to go find her."
		-- "Oh.. right." She frowns. "Please apologize for me when you see her.. I'm going to stay in my room for now, I think."
		(:setCharInfo "sera", "Sera":)
		>> go.eoc

	+ {intro_chase} [Chase after Erin.]
		You run down the hall after your sister, but once you get there, you realize that you're not actually sure which room it is that she went into.
		>> #east_hallway


## daemon
	= notebook
	Daemon is a bogeyman. He seems nice enough and is a fairly attractive man for being a supposed creature of nightmares. However, he says that's just a facade.
	<< END
	
	= intro
	~ if (%.daemon.intro_talk)
		Daemon is seated on a stool at the bar. He appears to be lost in his drink and doesn't even look up at you.
		>> go.back

	- Erin is seated at the bar. Beside her is a man that looks to be in his 30s. If he were human, that is, which may not be the case. Erin appears to be nursing a drink of some sort.

	+ [Talk to them.]
	+ [Go back.];; >> $:bookmark

	= intro_talk
	- "Oh! $:name!" She leaps from the stool and practically tackles you in a hug. "I was so scared! That spider was so <i>huge</i>!"

	~ if (%.sera.intro_chase)
		"I tried to tell her," the man says, "Sera is a nice girl. She may look scary, but she's a delicate flower, that one. She's more scared of you of than you are of her."
		Erin narrows her eyes. "... That's what people say about spiders."
		"That they do."
		(:setCharInfo "sera", "Sera":)
		(:endTask "main_2.find_sera":)
	~ else
		++ "She seems really nice though. She asked me to apologize for her for scaring you."
		-- "You talked with her!" She is utterly shocked. "Hooww?"

	+ "You might not like it, but she's living with us. You'll have to get used to her, Erin."
	- "Yeah.. Yeah, I know. It's just.. Why can't they all look normal like.. um.." She stops and turns to the man, her arm still around your waist. "I don't think I ever asked your name."
	The man just grins. "It's Daemon."
	"Yeah. Why can't they be normal like Daemon here?"
	Daemon laughs. "Girly, I'm just wearing my 'normal' face. If I showed you the real me, you wouldn't sleep for weeks. I wear this facade because even the myths here don't like looking at me."
	"... oh... okay." She says in a meek voice.
	(:endTask "main_2.find_daemon":)

	+ "What kind of myth are you?"
	- His face contorts into an evil grins. "I'm a bogeyman."
	"... oh.." says Erin, and hugs you tighter. "$:name, why are all of our tenants so scary? The fairy is the only not creepy one."
	"Finola?" Daemon chuckles. "Be careful of that one. She comes from the wrong side of the fairy tracks."
	"But I thought fairies were supposed to be good."
	"You watch too many cartoons."
	She frowns.
	"The media only shows the nicest of the four fairy factions. Finola aligns with the second darkest. So she's not outright evil, but I'd still watch your back."
	"Seoyun was saying that there myths aren't evil."
	"Every villain is the hero of their own story, right? It's like that," Daemon says with a shrug.

	+ "We should probably get our luggage up to our rooms now that we've met everyone."
	- "Mmmm. Chances are that Vanessa has beaten you to it, but I'll talk to you later anyway." He gives a polite nod and tosses back a drink.

	[:& $.erin_following = true:]
	(:setCharInfo "daemon", "Daemon":)
	(:addTask "main_3.unpack":)
	>> go.eoc
